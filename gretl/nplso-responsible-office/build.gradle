plugins {
    id "ch.so.agi.gretl" version "1.0.5-SNAPSHOT"
}

import ch.so.agi.gretl.tasks.*
import ch.so.agi.gretl.api.TransferSet

def GROUP_INIT_DB_NPLSO_RESP_OFFICES = "Erfassung zuständige Stellen für Nutzungsplanung (einmalig)"

task createSchemaOerebNutzungsplanungZustaendigeStelle(type: Ili2pgImportSchema) {
    description = "Erstellt ein OEREB-Vorschriften Schema zum einmaligen Erfassen/Abfüllen der zuständigen Stellen (kommunal und kantonal)."
    group = GROUP_INIT_DB_NPLSO_RESP_OFFICES
    database = [dbUriOereb, dbUserOereb, dbPwdOereb]
    models = iliModelTransferstruktur
    dbschema = dbSchemaOerebNutzungsplanungZustaendigeStelle
    nameByTopic = true
    createFk = false
    createFkIdx = true
    createUnique = false
    createNumChecks = false
    createEnumTabs = true
    createBasketCol = true
    createDatasetCol = true
    strokeArcs = true
    defaultSrsCode = 2056
    disableValidation = true
}

task deleteInsertZustaendigeStelle(type: SqlExecutor) {
    description = "Füllt eher Dummy-Daten für die zuständigen Stellen in die Tabelle 'Vorschriften_Amt' ein. Für Proof-of-Concept' reicht es. T_ili_tid ist matchentscheiden."
    group = GROUP_INIT_DB_NPLSO_RESP_OFFICES
    database = [dbUriOereb, dbUserOereb, dbPwdOereb]
    sqlFiles = ["delete_responsible_offices.sql","insert_responsible_offices.sql"]
}


task exportZustaendigeStelle(type: Ili2pgExport) {
    description = "Exportiert die zuständigen STellen in eine INTERLIS-Transferdatei."
    group = GROUP_INIT_DB_NPLSO_RESP_OFFICES
    database = [dbUriOereb, dbUserOereb, dbPwdOereb]
    models = iliModelTransferstruktur
    dbschema = dbSchemaOerebNutzungsplanungZustaendigeStelle
    dataFile = "ch.so.arp.npl.zustaendigestelle.xtf"
    disableValidation = true
}